TITLE:
Anxiety symptoms in RAMP and COPING
piecewise trajectory
No pairwise correlations

DATA:
  	FILE IS "/Users/kirstin/Dropbox/SGDP/RAMP/Projects/LongitudinalCommon/
  	data_clean/gad/gad.clean_merged_total_scores_reduced.csv";

VARIABLE:

	NAMES ARE
		id gad_t0 gad_t3 gad_t4 gad_t5
		gad_t6 gad_t7 gad_t8 gad_t9 gad_t10 gad_t11
		gad_t12 gad_t13 gad_t14 gad_t15 gad_t16 gad_t17;
				
	USEVARIABLES ARE
		gad_t0 gad_t3 gad_t4 gad_t5
		gad_t6 gad_t7 gad_t8 gad_t9 gad_t10 gad_t11
		gad_t12 gad_t13 gad_t14 gad_t15 gad_t16 gad_t17;
		
	MISSING ARE ALL (-99);
	IDVARIABLE IS id;
	
	
ANALYSIS:
	
	ESTIMATOR = MLR; !Maximum lifelihood with robust standard errors 
	!as data is skew for GAD at all time points
	PROCESSORS = 2; !only 2 on Mac
	ITERATIONS=5000;
	
MODEL:

!knot refers to point at which trajectory changes direction
!it is the start point for a trajectory, which corresponds to
!the end of the previous trajectory

!Piece 1 covering 07 April 2020 - 16 June (lockdown 1)
!Lockdown One till first easing 
!knot = baseline

	int lin1 | gad_t0@0 gad_t3@3 gad_t4@4
    		 gad_t5@5 gad_t6@5 gad_t7@5 gad_t8@5 gad_t9@5 
    		 gad_t10@5 gad_t11@5 gad_t12@5 gad_t13@5
    		 gad_t14@5 gad_t15@5 gad_t16@5 gad_t17@5;
	
!piece 2 covering 30 June 2020 (fup 6) to 20 Oct 2020 (fup 11)  	
!Summer easing of restrictions until start Lockdown 2.0
!Knot Follow up 5	 
    		
    	int lin2 | gad_t0@0 gad_t3@0 gad_t4@0
    		 gad_t5@0 gad_t6@1 gad_t7@2 gad_t8@3 gad_t9@4 
    		 gad_t10@6 gad_t11@8 gad_t12@8 gad_t13@8
    		 gad_t14@8 gad_t15@8 gad_t16@8 gad_t178;
	
!Piece 3 covering 17 November 2020 (Fup12) to 9 February 2021 (Fup 15)
!Winter lockdowns until spring 2021 easing of restrictions
!Knot follow up 11
    	int lin3 | gad_t0@0 gad_t3@0 gad_t4@0
    		 gad_t5@0 gad_t6@0 gad_t7@0 gad_t8@0 gad_t9@0 
    		 gad_t10@0 gad_t11@0 gad_t12@2 gad_t13@4
    		 gad_t14@6 gad_t15@8 gad_t16@8 gad_t17@8;
    		 
!Piece 4 covering  9 March 2021 (fup 16) to 6 April 2021 (fup 17)
!Spring 2021 easing of restrictions
!knot follow up 15
    	
    	int lin4 | gad_t0@0 gad_t3@0 gad_t4@0
    		 gad_t5@0 gad_t6@0 gad_t7@0 gad_t8@0 gad_t9@0 
    		 gad_t10@0 gad_t11@0 gad_t12@0 gad_t13@0
    		 gad_t14@0 gad_t15@0 gad_t16@2 gad_t17@4;
    		 
	
	int WITH lin1;
	int WITH lin2;
	int WITH lin3;
	int WITH lin4;
	
	lin1 WITH lin2;
	lin1 WITH lin3;
	lin1 WITH lin4;
	
	lin2 WITH lin3;
	lin2 WITH lin4;
	
	lin3 WITH lin4;
	
	
	
PLOT:
	TYPE = PLOT3; !do not get plots on Mac but we use the gh5 file in R
	

	SERIES = gad_t0(0) gad_t3(3) gad_t4(4)
    	gad_t5(5) gad_t6(6) gad_t7(7) gad_t8(8) gad_t9(9) gad_t10(11)
    	gad_t11(13) gad_t12(15) gad_t13(17) gad_t14(19) gad_t15(21) 
    	gad_t16(23) gad_t17(25);
    
    
	
OUTPUT:
	STDYX MODINDICES(30) RESIDUAL;

SAVEDATA:

   ! SAVE=FS;		
   ! FILE IS lgc-phq9-linear.dat;	
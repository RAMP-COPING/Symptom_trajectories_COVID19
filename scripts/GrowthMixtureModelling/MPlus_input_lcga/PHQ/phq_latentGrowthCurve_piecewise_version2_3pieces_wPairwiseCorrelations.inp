TITLE:
Depression symptoms in RAMP and COPING April 2020 - April 2021: 
piecwise trajectories based on lockdowns
v2, Not capturing Eary 2021 easing as it is too dsitributed
With pairwise correlations

DATA:
  	FILE IS "/Users/kirstin/Dropbox/SGDP/RAMP/Projects/LongitudinalCommon/
  	data_clean/phq/phq.clean_merged_total_scores_reduced.csv";

VARIABLE:

	NAMES ARE
		id phq_t0 phq_t3 phq_t4 phq_t5
		phq_t6 phq_t7 phq_t8 phq_t9 phq_t10 phq_t11
		phq_t12 phq_t13 phq_t14 phq_t15 phq_t16;
				
	USEVARIABLES ARE
		phq_t0 phq_t3 phq_t4 phq_t5
		phq_t6 phq_t7 phq_t8 phq_t9 phq_t10 phq_t11
		phq_t12 phq_t13 phq_t14 phq_t15 phq_t16;
		
	MISSING ARE ALL (-99);
	IDVARIABLE IS id;
	
	
ANALYSIS:
	
	ESTIMATOR = MLR; !Maximum lifelihood with robust standard errors 
	!as data is skew for PHQ at all time points
	PROCESSORS = 2; !only 2 on Mac
	COVERAGE=0;
	ITERATIONS=5000;
	
MODEL:
!Note, a single trajectory, so we retain a single intercept 
!and only introduce different slopes


!piece 1 covering 21 April - 30 June
!lockdown 1

	int lin1 | phq_t0@0 phq_t3@3 phq_t4@4
    		 phq_t5@5 phq_t6@6 phq_t7@6 phq_t8@6 phq_t9@6 
    		 phq_t10@6 phq_t11@6 phq_t12@6 phq_t13@6
    		 phq_t14@6 phq_t15@6 phq_t16@6;
	
!piece 2 covering 30 June (knot = timepoint 6) to 20 Dec  
! easing of restrictions		 
    		
    	int lin2 | phq_t0@0 phq_t3@0 phq_t4@0
    		 phq_t5@0 phq_t6@0 phq_t7@1 phq_t8@2 phq_t9@3 
    		 phq_t10@5 phq_t11@7 phq_t12@9 phq_t13@9
    		 phq_t14@9 phq_t15@9 phq_t16@9;
	
! Piece 3 covering 20 Dec to 5th April 2020
!lockdown 2 + 3 and starting of easing resrtrictions
    	int lin3 | phq_t0@0 phq_t3@0 phq_t4@0
    		 phq_t5@0 phq_t6@0 phq_t7@0 phq_t8@0 phq_t9@0 
    		 phq_t10@0 phq_t11@0 phq_t12@0 phq_t13@2
    		 phq_t14@4 phq_t15@6 phq_t16@8;
	
    		 
	
	int WITH lin1;
	int WITH lin2;
	int WITH lin3;

	
	lin1 WITH lin2;
	lin1 WITH lin3;

	
	lin2 WITH lin3;
	
	phq_t0 - phq_t15 PWITH phq_t3 - phq_t16;
	
	
PLOT:
	TYPE = PLOT3; !do not get plots on Mac but we use the gh5 file in R
	
	SERIES = phq_t0(0) phq_t3(3) phq_t4(4)
    	phq_t5(5) phq_t6(6) phq_t7(7) phq_t8(8) phq_t9(9) phq_t10(11)
    	phq_t11(13) phq_t12(15) phq_t13(17) phq_t14(19) phq_t15(21) 
    	phq_t16(23);
    
    
	
OUTPUT:
	STDYX MODINDICES(30) RESIDUAL;

SAVEDATA:

   ! SAVE=FS;		
   ! FILE IS lgc-phq9-linear.dat;	